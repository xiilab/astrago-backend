buildscript {
    ext {
        // restdocs-api-spec: version 변수 설정
        set('restdocsApiSpecVersion', '0.18.4')
        // swaggerUi: swaggerUI 문서 생성 위치 명시
        set('swaggerUi', file("src/main/resources/static/docs/swagger-ui"))
    }
}

plugins {
    // restdocs-api-spec: Spring REST Docs의 결과물을 OpenAPI3 스펙으로 변환
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    //swagger generator: OpenAPI3 스펙을 기반으로 SwaggerUI 생성
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

dependencies {
    implementation project(':module-k8s')
    implementation project(':module-user')
    implementation project(':module-common')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    // swagger
//    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // Spring Rest Docs + Swagger UI
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"
    swaggerUI 'org.webjars:swagger-ui:4.11.1'
    // -- Spring Rest Docs + Swagger UI

    // KeyCloak
    implementation 'org.keycloak:keycloak-admin-client:23.0.1'
    implementation 'org.keycloak:keycloak-spring-boot-starter:23.0.1'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
}

clean {
    delete swaggerUi
}

// OpenAPI 스펙 생성 설정
openapi3 {
    servers =[
            // TODO 배포할 서버 url도 추가해야됨
            {url = "http://localhost:8080/astrago"}
    ]
    title = "AstraGo Test API Documentation"
    description = "Spring REST Docs with SwaggerUI"
    version = "0.0.1"
    format = "yaml"
    outputFileNamePrefix = 'openapi3'
    // outputDirectory: openapi3.yaml이 저장될 위치, 명시하지 않으면 기본값인 "/build/api-spec" 경로에 저장됨
    // outputDirectory = "."
}

// swagger에서 html 생성할 수 있는 "build/api-spec/openapi3.yaml"의 위치를 알려줌
swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

// 생성된 SwaggerUI 를 jar에 포함시키기 위해 build/resources 경로로 복사
task copySwaggerUI(type: Copy) {
    dependsOn 'generateSwaggerUISample'

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()

    from file("${generateSwaggerUISampleTask.outputDir}")
    into swaggerUi
}

// bootJar 실행 전, copySwaggerUI 를 실행하도록 설정
bootJar {
    dependsOn 'copySwaggerUI'
    archivesBaseName = 'astrago'
    archiveFileName = 'astrago-0.0.1-SNAPSHOT.jar'
    archiveVersion = "0.0.1"
}

// GenerateSwaggerUI 태스크가, openapi3 task 를 의존하도록 설정
tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'
}


